000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. GTULRECL.
000300 AUTHOR. R THORNTON.
000400 DATE-WRITTEN.  JAN 2000.
000500 DATE-COMPILED.
000510*****************************************************************
000600*REMARKS. PROVIDES THE CALLER WITH THE LRECL FOR THE LAST RECORD*
000610*         READ FROM A DSORG=PS DATASET. CALLER MUST DEFINE THE  *
000620*         FILE AND ISSUE A READ BEFORE CALLING THIS ROUTINE.    *
000621*         A CALL CAN BE MADE FOLLOWING EACH READ IF DESIRED.    *
000622*         WHILE THE FILE CAN HAVE ANY DESIRED RECFM, IS IS MOST *
000623*         USEFUL FOR RECFM=U DATASETS, AS COBOL DOES NOT        *
000624*         PROVIDE THE LENGTH FOR UNDEFINED RECORDS.             *
000625*         THE CALLER PASSES THE ADDRESS OF THE DCB (COBOL FD)   *
000626*         AND A 2-BYTE BINARY FIELD IN WHICH THE LRECL IS       *
000627*         RETURNED.                                             *
000628*         COBOL EXAMPLE:                                        *
000629*                 .                                             *
000630*                 .                                             *
000631*                  FD UNDEF-INPUT-FILE                          *
000632*                     RECORDING MODE IS U                       *
000633*                 .                                             *
000634*                 .                                             *
000635*                     05  LRECL       PIC S9(4) COMP.           *
000636*                 .                                             *
000637*                 .                                             *
000638*                     OPEN INPUT UNDEF-INPUT-FILE.              *
000639*                 .                                             *
000640*                 .                                             *
000641*                     READ UNDEF-INPUT-FILE                     *
000642*                         AT END ...                            *
000643*                     CALL 'GTULRECL' USING UNDEF-INPUT-FILE,   *
000644*                                           LRECL.              *
000645*                                                               *
000650*****************************************************************
000700 ENVIRONMENT DIVISION.
000800 INPUT-OUTPUT SECTION.
000900 FILE-CONTROL.
001200 DATA DIVISION.
001300 FILE SECTION.
003100 WORKING-STORAGE SECTION.
003200 77  FILLER                             PIC X(44)
003300       VALUE 'GTULRECL WORKING STORAGE SECTION STARTS HERE'.
004800 LINKAGE SECTION.
004900 01  FD-DESCRIPTION.
005000     05  FILLER                         PIC X(16).
005100     05  KEY-LENGTH-BINARY              PIC X.
005200     05  DEVICE-TYPE-HEX                PIC X.
005210     05  FILLER                         PIC X(2).
005300     05  NUMBER-BUFFERS-BINARY          PIC X.
007710     05  BUFFER-POOL-CTRL-BLOCK-ADDRESS PIC X(3).
007720     05  BUFFER-LENGTH-BINARY           PIC S9(4) COMP.
007730     05  DSORG-HEX                      PIC X(2).
007731     05  FILLER                         PIC X.
007740     05  CHAINED-SCHED-IOB-ADDRESS      PIC X(3).
007750     05  FILLER                         PIC X.
007760     05  END-OF-DATA-ADDRESS            PIC X(3).
007770     05  RECFM-HEX                      PIC X.
007771     05  EXIT-LIST-ADDRESS              PIC X(3).
007772     05  TIOT-OFFSET-BINARY             PIC S9(4) COMP.
007773     05  MACRF-HEX                      PIC X(2).
007774     05  FILLER                         PIC X.
007775     05  DEB-ADDRESS                    PIC X(3).
007776     05  OPEN-FLAGS-HEX                 PIC X.
007777     05  INPUT-OUTPUT-MODULE-ADDRESS    PIC X(3).
007778     05  OPTION-CODES-HEX               PIC X.
007779     05  CHECK-SYNCH-ROUTINE-ADDRESS    PIC X(3).
007780     05  IOB-LENGTH-HALFWORDS-BINARY    PIC X.
007781     05  SYNAD-ROUTINE-ADDRESS          PIC X(3).
007782     05  CONDITION-INDICATORS-HEX       PIC X(2).
007783     05  BLOCK-SIZE-BINARY              PIC S9(4) COMP.
007784     05  WRITE-CHANNEL-PGM-OFFSET-BIN   PIC X.
007785     05  WRITE-CHANNEL-PGM-LENGTH-BIN   PIC X.
007786     05  READ-CHANNEL-PGM-OFFSET-BIN    PIC X.
007787     05  WRITE-CCW-OFFSET-BINARY        PIC X.
007788     05  FILLER                         PIC X.
007789     05  NORMAL-SCHED-IOB-PREFIX-ADDR   PIC X(3).
007790     05  LAST-BYTE-CURRENT-BUFFER-ADDR  PIC X(4).
007791     05  FILLER                         PIC X.
007792     05  CURRENT-NEXT-RECORD-ADDRESS    PIC X(3).
007793     05  FLAG-BYTE-HEX                  PIC X.
007794     05  BYTES-USED-LAST-DIR-BLK-BIN    PIC X.
007795     05  LRECL-BINARY                   PIC S9(4) COMP.
007796     05  ERROR-OPTIONS-HEX              PIC X.
007797     05  CONTROL-MODULE-ADDRESS         PIC X(3).
007798     05  FILLER                         PIC XX.
007799     05  BLOCK-MAX-BLOCK-LENGTH-BINARY  PIC S9(4) COMP.
007800     05  END-OF-BLOCK-MODULE-ADDRESS    PIC X(4).
007801*
007802 01  LRECL                              PIC S9(4) COMP.
007803*
007804 PROCEDURE DIVISION USING FD-DESCRIPTION,
007805                          LRECL.
007900*
008000 A000-MAINLINE.
008100     MOVE LRECL-BINARY TO LRECL.
008400     GOBACK.
