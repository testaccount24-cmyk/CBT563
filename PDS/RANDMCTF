000010 IDENTIFICATION DIVISION.
000020 PROGRAM-ID. RANDMCTF.
000030 AUTHOR. RICHARD THORNTON
000040 DATE-WRITTEN. MARCH, 2000.
000050 DATE-COMPILED.
000060       *********************************************************
000061       * THE RANDMCTF SUBROUTINE PROVIDES RANDOM READ ACCESS TO*
000062       * THE MASTER CODE TABLE FILE.                           *
000066       *                                                       *
000067       * THIS VERSION WAS REWRITTEN IN COBOL II USING A DIS-   *
000070       * ASSEMBLY OF THE ORIGINAL LOAD MODULE AS A GUIDELINE.  *
000071       * IT IS COMPATIBLE WITH THE COBOL AND LANGUAGE ENVIRON- *
000072       * MENT IN USE AT TRIGON IN MARCH, 2000.                 *
000073       *                                                       *
000080       * THE SOURCE PROGRAM FORMERLY IN ENDEVOR WAS AN ASSEMBLY*
000090       * LANGUAGE DISASSEMBLY OF THE LOAD MODULE IN PRODUCTION,*
000100       * SINCE THE ORIGINAL SOURCE HAD BEEN LOST AT THE TIME OF*
000110       * CONVERSION TO ENDEVOR.                                *
000111       *                                                       *
000120       * THE ORIGINAL LOAD MODULE WAS COMPILED WITH VS COBOL   *
000130       * R2.3 AND THE OLD CAPEX OPTIMIZER.                     *
000150       *********************************************************
000250       * WHEN CALLED FROM A COBOL PROGRAM, THERE ARE THREE     *
000251       * PARAMETERS TO BE PASSED. SAMPLE CODE FOR THE DATA AND *
000252       * CALL:                                                 *
000254       *                                                       *
000260       *       05  IOOP              PIC X.                    *
000280       *           88  RANDMCTF-OPEN  VALUE 'O'.               *
000290       *           88  RANDMCTF-CLOSE VALUE 'C'.               *
000300       *           88  RANDMCTF-READ  VALUE 'R'.               *
000410       *       05  KEY-VALUE         PIC X(15).                *
000441       *       05  RETURN-AREA.                                *
000442       *           10  RANDMCTF-RETURN-CODE  PIC X.            *
000450       *               88  RANDMCTF-GOOD-READ       VALUE ' '. *
000451       *               88  RANDMCTF-NO-RECORD-FOUND VALUE '*'. *
000460       *               88  RANDMCTF-ERROR           VALUE 'E'. *
000461       *           10  MCTF-RECORD-BYTE-1    PIC X.            *
000462       *           10  MCTF-RECORD-KEY       PIC X(15).        *
000463       *           10  MCTF-RECORD-BYTE-1    PIC X(195).       *
000465       *       .                                               *
000466       *       .                                               *
000467       *       .                                               *
000468       *       .                                               *
000469       *       MOVE 'O' TO IOOP.                               *
000470       *       MOVE SPACES TO KEY-VALUE, RETURN-AREA.          *
000471       *       CALL 'RANDMCTF' USING IOOP,                     *
000472       *                       KEY-VALUE,                      *
000473       *                       RETURN-AREA.                    *
000474       *                                                       *
000475       *       .                                               *
000476       *       .                                               *
000477       *       .                                               *
000478       *       .                                               *
000479       *       MOVE 'R' TO IOOP.                               *
000480       *       MOVE KEY-OF-RECORD-DESIRED TO KEY-VALUE         *
000481       *       MOVE SPACES TO RETURN-AREA.                     *
000482       *       CALL 'RANDMCTF' USING IOOP,                     *
000483       *                       KEY-VALUE,                      *
000484       *                       RETURN-AREA.                    *
000485       *       IF RANDMCTF-GOOD-READ                           *
000486       *          GO TO PROCESS-RECORD                         *
000487       *       ELSE                                            *
000488       *           IF RANDMCTF-NO-RECORD-FOUND                 *
000489       *               GO TO RECORD-NOT-FOUND                  *
000490       *           ELSE                                        *
000491       *               GO TO HANDLE-RANDMCTF-ERRORS.           *
000492       *       .                                               *
000493       *       .                                               *
000494       *       .                                               *
000495       *       .                                               *
000496       *       MOVE 'C' TO IOOP.                               *
000497       *       MOVE SPACES TO KEY-VALUE, RETURN-AREA.          *
000498       *       CALL 'RANDMCTF' USING IOOP,                     *
000499       *                       KEY-VALUE,                      *
000500       *                       RETURN-AREA.                    *
000510       *********************************************************
000600
000708 ENVIRONMENT DIVISION.
000709 INPUT-OUTPUT SECTION.
000710 FILE-CONTROL.
000711     SELECT MASTER-CODE-TABLE-FILE ASSIGN TO UT-MCTF
000712         ACCESS IS RANDOM
000713         ORGANIZATION IS INDEXED
000714         RECORD KEY IS MCTF-KEY
000715         FILE STATUS IS WS-MCTF-STATUS-CODE.
000716
000717 DATA DIVISION.
000718 FILE SECTION.
000719 FD  MASTER-CODE-TABLE-FILE
000721     DATA RECORD IS MCTF-RECORD.
000722 01  MCTF-RECORD.
000723     05  FILLER                      PIC X.
000724     05  MCTF-KEY                    PIC X(15).
000725     05  MCTF-DATA                   PIC X(195).
000726
000727 WORKING-STORAGE SECTION.
000729 01  FILLER                          PIC X(36)
000730         VALUE  'RANDMCTF WORKING STORAGE BEGINS HERE'.
000732 01  WS-OPEN-SWITCH                  PIC X.
000733 01  WS-MCTF-STATUS-CODE             PIC X(2) VALUE ZEROS.
000734 01  WS-MCTF-RECORD.
000735     05  WS-MCTF-DELETE-CODE         PIC X VALUE SPACES.
000736     05  WS-MCTF-KEY                 PIC X(15) VALUE SPACES.
000737     05  WS-MCTF-DATA                PIC X(195) VALUE SPACES.
001091
001100 LINKAGE SECTION.
001110 01  PARM-IOOP                       PIC X(1).
001111
001112 01  PARM-MCTF-KEY                   PIC X(15).
001115
001120 01  PARM-MCTF-RETURN-AREA.
001121     05  PARM-ERR                    PIC X(1).
001122         88  PARM-GOOD-COMPLETION    VALUE ' '.
001123         88  PARM-NO-RECORD-FOUND    VALUE '*'.
001124         88  PARM-ERROR-COMPLETION   VALUE 'E'.
001125     05  PARM-MCTF-RECORD.
001126         10  PARM-MCTF-DELETE-CODE   PIC X.
001130         10  PARM-MCTF-RECORD-KEY    PIC X(15).
001140         10  PARM-MCTF-RECORD-DATA   PIC X(195).
001170
001210 PROCEDURE DIVISION  USING PARM-IOOP, PARM-MCTF-KEY,
001220                           PARM-MCTF-RETURN-AREA.
001230 A100-EXECUTIVE-ROUTINE.
001241     IF PARM-IOOP = 'R'                                                 9
001250        IF WS-OPEN-SWITCH = '1'
001260           PERFORM B100-READ-MCTF THRU B100-EXIT
001270        ELSE
001280           MOVE 'E' TO PARM-ERR
001282     ELSE
001283         IF PARM-IOOP = 'O'
001284             PERFORM C100-OPEN-MCTF THRU C100-EXIT
001285         ELSE
001286             IF PARM-IOOP = 'C'
001287                 PERFORM D100-CLOSE-MCTF THRU D100-EXIT
001288             ELSE
001289                 MOVE 'E' TO PARM-ERR.
001290     GOBACK.
001291
001292 B100-READ-MCTF.
001293     MOVE PARM-MCTF-KEY TO MCTF-KEY.
001294     READ MASTER-CODE-TABLE-FILE INTO WS-MCTF-RECORD.
001295     IF WS-MCTF-STATUS-CODE = '00'
001296         MOVE WS-MCTF-RECORD TO PARM-MCTF-RECORD
001297         MOVE ' ' TO PARM-ERR
001298         MOVE ' ' TO PARM-MCTF-DELETE-CODE
001299     ELSE
001300         IF WS-MCTF-STATUS-CODE = '23'
001301             MOVE '*' TO PARM-ERR
001302         ELSE
001303             MOVE 'E' TO PARM-ERR.
001304 B100-EXIT. EXIT.
001305
001312 C100-OPEN-MCTF.
001313     OPEN INPUT MASTER-CODE-TABLE-FILE.
001314     MOVE '1' TO WS-OPEN-SWITCH.
001315 C100-EXIT. EXIT.
001316
001317 D100-CLOSE-MCTF.
001318        CLOSE MASTER-CODE-TABLE-FILE.
001319        MOVE '0' TO WS-OPEN-SWITCH.
001320 D100-EXIT. EXIT.
