       IDENTIFICATION DIVISION.
       PROGRAM-ID.    DATES.
       AUTHOR. R THORNTON
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *   DATES READS ITS SYSIN FILE TO OBTAIN A DATE TO BE CONVERTED,*
      *   WHICH IS PASSED TO GENLDAT2. ON RETURN FROM GENLDAT2, THE   *
      *   RETURN CODE IS TESTED FOR SUCCESSFUL CONVERSION. IF NOT     *
      *   SUCCESSFUL, AN ERROR MESSAGE IS WRITTEN TO THE SYSPRINT FILE*
      *   OTHERWISE THE CONVERSION DATA IS REFORMATTED AND WRITTEN TO *
      *   THE SYSPRINT FILE.                                          *
      *
      *   THE SYSIN DATE INFORMATION IS ENTERED BEGINNING IN COLUMN 1 *
      *   AS JYYYYDDD FOR JULIAN DATES OR AS GMMDDYYYY FOR GREGORIAN. *
      *
      *  AUTHOR R. THORNTON  JAN 2000
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT PRINT-FILE ASSIGN TO UT-S-SYSPRINT.
           SELECT CARD-FILE ASSIGN TO UT-S-SYSIN.
       DATA DIVISION.
       FILE SECTION.
       FD  PRINT-FILE
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORDS ARE STANDARD.
       01  PRINT-FILE-RECORD       PIC X(72).

       FD  CARD-FILE
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORDS ARE STANDARD.
       01  CARD-FILE-RECORD.
           05  CARD-DATE-TYPE            PIC X(01).
           05  CARD-GREGORIAN-DATE       PIC 9(08).
           05  FILLER REDEFINES CARD-GREGORIAN-DATE.
               10  CARD-JULIAN-DATE      PIC 9(07).
               10  FILLER                PIC X(01).
           05  FILLER                    PIC X(71).
       01  END-CARD-RECORD.
           05 END-CARD                   PIC X(3).
           05 FILLER                     PIC X(77).

       WORKING-STORAGE SECTION.
       77  FILLER PIC X(36)  VALUE
           'DATES WORKING STORAGE BEGINS HERE'.

       01  SWITCHES.
           05  EOF-SWITCH                PIC X(01) VALUE 'N'.
               88  MORE-CARDS                      VALUE 'N'.
               88  NO-MORE-CARDS                   VALUE 'Y'.
           05  ERROR-SWITCH              PIC X(01) VALUE 'N'.
               88  NO-ERROR-FOUND                  VALUE 'N'.
               88  ERROR-FOUND                     VALUE 'Y'.

       01  GENLDAT2-RETURN-AREA.
           05  DATE-TYPE                 PIC X(01).
               88  JULIAN-SUPPLIED                 VALUE 'J'.
               88  GREGORIAN-MDY-SUPPLIED          VALUE 'G'.
           05  DATE-ERROR                PIC X(01).
               88  NO-DATE-ERROR                   VALUE SPACE.
               88  YES-DATE-ERROR                  VALUE 'E'.
           05  DAY-OF-THE-WEEK           PIC 9(01).
               88  MONDAY                          VALUE 1.
               88  TUESDAY                         VALUE 2.
               88  WEDNESDAY                       VALUE 3.
               88  THURSDAY                        VALUE 4.
               88  FRIDAY                          VALUE 5.
               88  SATURDAY                        VALUE 6.
               88  SUNDAY                          VALUE 7.
           05  HOLIDAY-INDICATOR         PIC X(01).
               88  NOT-A-HOLIDAY                   VALUE ' '.
               88  NEW-YEARS-DAY                   VALUE '1'.
               88  GOOD-FRIDAY                     VALUE '3'.
               88  MEMORIAL-DAY                    VALUE '4'.
               88  INDEPENDENCE-DAY                VALUE '5'.
               88  LABOR-DAY                       VALUE '6'.
               88  THANKSGIVING-DAY                VALUE '7'.
               88  THANKSGIVING-FRIDAY             VALUE '8'.
               88  CHRISTMAS-DAY                   VALUE '9'.
           05  YEAR-TYPE                 PIC X(01).
               88  NOT-LEAP-YEAR                   VALUE '0'.
               88  LEAP-YEAR                       VALUE '1'.
           05  JULIAN-DATE               PIC 9(07) COMP-3.
           05  GREGORIAN-MDY-DATE.
               10  GREG-MDY-MONTH-DAY.
                   15  GREG-MDY-MONTH    PIC 99.
                   15  GREG-MDY-DAY      PIC 99.
               10  GREG-MDY-YEAR         PIC 9(4).
           05  GREGORIAN-YMD-DATE.
               10  GREG-YMD-YEAR         PIC 9(4).
               10  GREG-YMD-MONTH        PIC 99.
               10  GREG-YMD-DAY          PIC 99.
           05  LITERAL-DATE              PIC X(18).
           05  JULIAN-SLASH.
               10  JULIAN-SLASH-YEAR     PIC 9(4).
               10  JULIAN-SLASH-SLASH    PIC X.
               10  JULIAN-SLASH-DAY      PIC 999.
           05  GREGORIAN-SLASH.
               10  GREG-SLASH-MONTH      PIC 99.
               10  GREG-SLASH-SLASH1     PIC X.
               10  GREG-SLASH-DAY        PIC 99.
               10  GREG-SLASH-SLASH2     PIC X.
               10  GREG-SLASH-YEAR       PIC 9(4).
           05  DAY-LITERAL               PIC X(09).
           05  MONTH-LITERAL             PIC X(09).


       01  PRT-RETURN-AREA.
           05  PRT-LEAP-YEAR-LINE.
               10  FILLER                 PIC X VALUE '('.
               10  PRT-LEAP-YEAR          PIC X(4).
               10  FILLER          PIC X(16) VALUE ' IS A LEAP YEAR.'.
               10  FILLER                 PIC X VALUE ')'.
           05  PRT-NOT-LEAP-YEAR-LINE.
               10  FILLER                 PIC X VALUE '('.
               10  PRT-NOT-LEAP-YEAR      PIC X(4).
               10  FILLER      PIC X(20) VALUE ' IS NOT A LEAP YEAR.'.
               10  FILLER                 PIC X VALUE ')'.
           05  PRT-LITERAL-DATE-LINE.
               10  PRT-DAY-LITERAL        PIC X(9).
               10  FILLER                 PIC X VALUE ' '.
               10  PRT-LITERAL-DATE       PIC X(18).
               10  FILLER                 PIC X(4) VALUE SPACES.
               10  PRT-LITERAL-LEAP-YEAR  PIC X(26).
           05  PRT-HOLIDAY-INDICATOR-LINE.
               10  FILLER       PIC X(9) VALUE 'HOLIDAY: '.
               10  PRT-HOLIDAY-INDICATOR-MEANING PIC X(20).
           05  PRT-DATES-LINE.
               10  FILLER      PIC X(15) VALUE 'JULIAN SLASH = '.
               10  PRT-JULIAN-SLASH       PIC X(8).
               10  FILLER      PIC X(4) VALUE SPACES.
               10  FILLER      PIC X(18) VALUE 'GREGORIAN SLASH = '.
               10  PRT-GREGORIAN-SLASH    PIC X(10).

       01  PRINT-MESSAGES.
           05  BAD-CARD-TYPE              PIC X(45) VALUE
                 'CARD BELOW HAS AN INVALID DATE TYPE IN COL 1:'.
           05  BAD-CARD-DATE             PIC X(31) VALUE
                 'CARD BELOW HAS AN INVALID DATE:'.
           05  PRT-SEPARATOR-LINE.
               10  FILLER     PIC X(12) VALUE '-.-.-.-.-.-.'.
               10  FILLER     PIC X(12) VALUE '-.-.-.-.-.-.'.
               10  FILLER     PIC X(12) VALUE '-.-.-.-.-.-.'.
               10  FILLER     PIC X(12) VALUE '-.-.-.-.-.-.'.
               10  FILLER     PIC X(12) VALUE '-.-.-.-.-.-.'.
               10  FILLER     PIC X(12) VALUE '-.-.-.-.-.-.'.

       01  LITERAL-TABLES.
           05  SUB                       PIC S9(5) COMP-3.
           05  DAY-OF-WEEK-LITERALS.
               10  FILLER                PIC X(10) VALUE 'MONDAY)'.
               10  FILLER                PIC X(10) VALUE 'TUESDAY)'.
               10  FILLER                PIC X(10) VALUE 'WEDNESDAY)'.
               10  FILLER                PIC X(10) VALUE 'THURSDAY)'.
               10  FILLER                PIC X(10) VALUE 'FRIDAY)'.
               10  FILLER                PIC X(10) VALUE 'SATURDAY)'.
               10  FILLER                PIC X(10) VALUE 'SUNDAY)'.
               10  FILLER                PIC X(10) VALUE '?????????)'.
           05  DAY-OF-WEEK-LITERAL REDEFINES DAY-OF-WEEK-LITERALS
                                         PIC X(10) OCCURS 8 TIMES.
           05  HOLIDAY-LITERALS.
               10  FILLER      PIC X(20) VALUE 'NEW YEARS DAY'.
               10  FILLER      PIC X(20) VALUE '?????????????'.
               10  FILLER      PIC X(20) VALUE 'GOOD FRIDAY'.
               10  FILLER      PIC X(20) VALUE 'MEMORIAL DAY'.
               10  FILLER      PIC X(20) VALUE 'INDEPENDENCE DAY'.
               10  FILLER      PIC X(20) VALUE 'LABOR DAY'.
               10  FILLER      PIC X(20) VALUE 'THANKSGIVING DAY'.
               10  FILLER      PIC X(20) VALUE 'THANKSGIVING FRIDAY'.
               10  FILLER      PIC X(20) VALUE 'CHRISTMAS DAY'.
               10  FILLER      PIC X(20) VALUE 'NOT A HOLIDAY'.
               10  FILLER      PIC X(20) VALUE '?????????????'.
           05  HOLIDAY-LITERAL REDEFINES HOLIDAY-LITERALS
                               PIC X(20) OCCURS 11 TIMES.
       PROCEDURE DIVISION.
           OPEN OUTPUT PRINT-FILE, INPUT CARD-FILE.
           WRITE PRINT-FILE-RECORD FROM PRT-SEPARATOR-LINE.
           PERFORM 1000-MAINLINE THRU 1000-EXIT
               UNTIL NO-MORE-CARDS.
           CLOSE PRINT-FILE, CARD-FILE.
           GOBACK.

       1000-MAINLINE.
           MOVE 'N' TO ERROR-SWITCH.
           READ CARD-FILE
               AT END MOVE 'Y' TO EOF-SWITCH
               GO TO 1000-EXIT.
           IF END-CARD = 'END'
               MOVE 'Y' TO EOF-SWITCH
               GO TO 1000-EXIT.
           MOVE LOW-VALUES TO GENLDAT2-RETURN-AREA
           MOVE CARD-DATE-TYPE TO DATE-TYPE
           IF DATE-TYPE = 'J'
                   MOVE CARD-JULIAN-DATE TO JULIAN-DATE
               ELSE IF DATE-TYPE = 'G'
                   MOVE CARD-GREGORIAN-DATE TO GREGORIAN-MDY-DATE
               ELSE
                   MOVE 'Y' TO ERROR-SWITCH
                   WRITE PRINT-FILE-RECORD FROM BAD-CARD-TYPE
                   WRITE PRINT-FILE-RECORD FROM CARD-FILE-RECORD
                   WRITE PRINT-FILE-RECORD FROM PRT-SEPARATOR-LINE
                   GO TO 1000-EXIT.
           CALL 'GENLDAT2' USING GENLDAT2-RETURN-AREA.
           IF DATE-ERROR = ' '
               NEXT SENTENCE
           ELSE
               WRITE PRINT-FILE-RECORD FROM BAD-CARD-DATE
               WRITE PRINT-FILE-RECORD FROM CARD-FILE-RECORD
               WRITE PRINT-FILE-RECORD FROM PRT-SEPARATOR-LINE
               GO TO 1000-EXIT.
           IF YEAR-TYPE = '0'
               MOVE GREG-MDY-YEAR TO PRT-NOT-LEAP-YEAR
               MOVE PRT-NOT-LEAP-YEAR-LINE TO PRT-LITERAL-LEAP-YEAR
           ELSE
               MOVE GREG-MDY-YEAR TO PRT-LEAP-YEAR
               MOVE PRT-LEAP-YEAR-LINE TO PRT-LITERAL-LEAP-YEAR.
           MOVE DAY-LITERAL TO PRT-DAY-LITERAL.
           MOVE LITERAL-DATE TO PRT-LITERAL-DATE.
           WRITE PRINT-FILE-RECORD FROM PRT-LITERAL-DATE-LINE.
           MOVE JULIAN-SLASH TO PRT-JULIAN-SLASH.
           MOVE GREGORIAN-SLASH TO PRT-GREGORIAN-SLASH.
           WRITE PRINT-FILE-RECORD FROM PRT-DATES-LINE.
           IF NOT-A-HOLIDAY
               NEXT SENTENCE
           ELSE
               MOVE HOLIDAY-INDICATOR TO SUB
               MOVE HOLIDAY-LITERAL (SUB) TO
                   PRT-HOLIDAY-INDICATOR-MEANING
               WRITE PRINT-FILE-RECORD FROM PRT-HOLIDAY-INDICATOR-LINE.
           WRITE PRINT-FILE-RECORD FROM PRT-SEPARATOR-LINE.
       1000-EXIT.
           EXIT.
