       IDENTIFICATION DIVISION.
       PROGRAM-ID.    ABNLTRM.
       AUTHOR. R THORNTON
      *REMARKS. ABNORMAL TERMINATION PROGRAM. PARM FIELD SPECIFIES THE
      *         DESIRED ABEND, S0C1, SOC4, SOC7, S0CB.
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT INPUT-FILE ASSIGN TO INPUT1.
           SELECT PRINT-FILE ASSIGN TO PRINT1.
       DATA DIVISION.
       FILE SECTION.
       FD  INPUT-FILE
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORDS ARE STANDARD.
       01  INPUT-RECORD.                                                      10
         05  IN-FIELD-1            PIC XX.                                    20
         05  IN-FIELD-2            PIC X(29).                                 30
         05  FILLER                PIC X(49).                                 40

       FD  PRINT-FILE
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORDS ARE STANDARD.
       01  PRINT-RECORD.                                                      10
         05  PRT-FIELD-1           PIC XX.                                    20
         05  PRT-FIELD-2           PIC X(29).                                 30
         05  FILLER                PIC X(49).                                 40

       WORKING-STORAGE SECTION.
       77  FILLER PIC X(36)  VALUE
           'ABNLTRM WORKING STORAGE BEGINS HERE'.
       01  MISCELLANEOUS-FIELDS.
           05  PACKED-FIELD        PIC S9(5)V99 COMP-3.
           05  SUB                 PIC S9(5) COMP-3.
           05  EOF-SWITCH          PIC X VALUE 'N'.
               88 END-OF-INPUT-FILE VALUE 'Y'.
           05  NUMBER-FIELD        OCCURS 10 TIMES
                                   PIC S9(4) COMP.

       LINKAGE SECTION.
       01  PARM-FIELD-ON-EXECUTE.
           05  FILLER                  PIC S9(4) COMP.
           05  ABEND-REQUEST           PIC X(4).

       PROCEDURE DIVISION USING PARM-FIELD-ON-EXECUTE.

       A100-EXECUTIVE-CONTROL.
           PERFORM A100-INITIALIZATION.
           PERFORM B100-MAINLINE-PROCESSING.
           PERFORM Z100-END-OF-PROCESSING.
           GOBACK.

       A100-INITIALIZATION.
           OPEN INPUT INPUT-FILE,
                OUTPUT PRINT-FILE.

       B100-MAINLINE-PROCESSING.
           PERFORM C100-READ.
           IF ABEND-REQUEST = 'S0C1'
               PERFORM F100-S0C1.
           IF ABEND-REQUEST = 'S0C4'
               PERFORM G100-S0C4.
           IF ABEND-REQUEST = 'S0C7'
               PERFORM J100-S0C7.
           IF ABEND-REQUEST = 'S0CB'
               PERFORM K100-S0CB.
           PERFORM W100-PRINT.
       C100-READ.
           READ INPUT-FILE
               AT END MOVE 'Y' TO EOF-SWITCH.

       F100-S0C1.
           CALL 'NONESUCH' USING ABEND-REQUEST.

       G100-S0C4.
           PERFORM H100-SUB-REFER VARYING SUB FROM 1 BY 1
               UNTIL SUB = 0.

       H100-SUB-REFER.
           MOVE NUMBER-FIELD (SUB) TO PACKED-FIELD.

       J100-S0C7.
           MOVE LOW-VALUES TO MISCELLANEOUS-FIELDS.
           ADD 1 TO PACKED-FIELD.

       K100-S0CB.
           MOVE 1000.00 TO PACKED-FIELD.
           MOVE 0 TO SUB.
           DIVIDE SUB INTO PACKED-FIELD.

       W100-PRINT.
           WRITE PRINT-RECORD AFTER ADVANCING 1 LINE.

       Z100-END-OF-PROCESSING.
           CLOSE INPUT-FILE, PRINT-FILE.
