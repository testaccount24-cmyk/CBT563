000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.  CTRLBLOX.
000300 AUTHOR.  R THORNTON.
000400 INSTALLATION.  BLUE CROSS AND BLUE SHIELD OF VIRGINIA.
000500 DATE-WRITTEN.  OCT, 1987.
000600 DATE-COMPILED.
000700****************************************************************
000800* TEST COBOL'S ABILITY TO ACCESS STORAGE IN UNUSUAL LOCATIONS, *
000900* IN ORDER TO OBTAIN INFORMATION FROM CONTROL BLOCKS WITHOUT   *
001000* HAVING TO RESORT TO USE OF ASSEMBLY LANGUAGE SUBROUTINES.    *
001100* THE FOLLOWING CHARACTERISTICS OF IBM COBOL ARE USED FOR THIS *
001200* TECHNIQUE:                                                   *
001300*                                                              *
001400*   1. IN LINKAGE SECTION, BLL CELLS FOR EACH RECORD ARE SET TO*
001500*      ZERO, AND ARE CHANGED ONLY WHEN ACCEPTED BY THE USING   *
001600*      PARAMETER ON THE PROCEDURE DIVISION, OR AN ENTRY STATE- *
001700*      MENT. THE RECORD MEMORY-BYTES IS NEVER RECEIVED, AND    *
001800*      WILL THEREFORE ALWAYS BE LOCATED AT ABSOLUTE LOCATION   *
001900*      ZERO, THE BEGINNING OF MEMORY.                          *
002000*                                                              *
002100*   2. SUBSCRIPT VALUES ARE NOT CHECKED FOR OUT-OF-RANGE VALUES*
002200*      IN NORMAL OPERATION, THEREFORE ANY VALUE AT ALL CAN BE  *
002300*      USED IN THE INDEX BYTE-NBR. BECAUSE OF THIS, ANY BYTE IN*
002400*      MEMORY CAN BE ACCESSED BY PUTTING ITS ADDRESS IN THE    *
002500*      INDEX FIELD BYTE-NBR, AND ACCESSING IT BY REFERRING TO  *
002600*      MEMORY-BYTE (BYTE-NBR).                                 *
002700*                                                              *
002800*   3. SINCE COBOL'S SUBSCRIPT/INDEXING FACILITY REFERS TO THE *
002900*      OCCURRENCE NUMBER OF A FIELD RELATIVE TO 1, RATHER THAN *
003000*      TO 0 AS IS NORMALLY GIVEN IN CONTROL BLOCK BOOKS, THE   *
003100*      SUBSCRIPT/INDEX VALUES MUST BE 1 GREATER THAN GIVEN IN  *
003200*      THE CONTROL BLOCK BOOKS. (TO ACCESS A FIELD AT OFFSET   *
003300*      67 RELATIVE TO ZERO, THE SUBSCRIPT OR INDEX MUST SPECIFY*
003400*      68).                                                    *
003500*                                                              *
003600*                                                              *
003700* AUTHOR R THORNTON NOV 1987                                   *
003800****************************************************************
003900 ENVIRONMENT DIVISION.
004000 CONFIGURATION SECTION.
004100 INPUT-OUTPUT SECTION.
004200 FILE-CONTROL.
004300     SELECT PRINT-OUT ASSIGN TO UT-S-PRINT1.
004400 DATA DIVISION.
004500 FILE SECTION.
004600 FD  PRINT-OUT
004700       LABEL RECORDS ARE STANDARD
004800       RECORD CONTAINS 80 CHARACTERS
004900       BLOCK CONTAINS 0 RECORDS
005000       DATA RECORD IS PRINT-LINE.
005100 01  PRINT-LINE.
005200     05  PRINT-DATE         PIC 99/999.
005300     05  FILLER             PIC X(74).
005400 WORKING-STORAGE SECTION.
005500 77  FILLER                     PIC X(36)  VALUE
005600     'CTRLBLOX WORKING STORAGE BEGINS HERE'.
005700 01  MISCELLANEOUS-WORK.
005800     05  CVTADR                 PIC S9(8) COMP.
005900     05  POINTER-WORK           PIC S9(8) COMP.
006000     05  FILLER REDEFINES POINTER-WORK.
006100         10  POINTER-HIGH-BYTE  PIC X.
006200         10  POINTER-BYTE2      PIC X.
006300         10  POINTER-BYTE3      PIC X.
006400         10  POINTER-BYTE4      PIC X.
006500     05  DATE-PACKED            PIC S9(7) COMP-3.
006600     05  FILLER REDEFINES DATE-PACKED.
006700         10 DATE-BYTE1          PIC X.
006800         10 DATE-BYTE2          PIC X.
006900         10 DATE-BYTE3          PIC X.
007000         10 DATE-BYTE4          PIC X.
007100 LINKAGE SECTION.
007200 01  MEMORY-BYTES.
007300     05  MEMORY-BYTE            PIC X OCCURS 4096 TIMES
007400                                INDEXED BY BYTE-NBR.
007500 PROCEDURE DIVISION.
007600****************************************************************
007700* INITIALIZE                                                   *
007800****************************************************************
007900     OPEN OUTPUT PRINT-OUT.
008000     MOVE SPACES TO PRINT-LINE.
008100****************************************************************
008200* GET THE ADDRESS OF THE CVT INTO CVTADR. THE CVT ADDRESS IS   *
008300* LOCATED AT ABSOLUTE MEMORY ADDRESSES 17-19 (DECIMAL).        *
008400****************************************************************
008500     MOVE LOW-VALUES TO POINTER-HIGH-BYTE.
008600     MOVE MEMORY-BYTE (18) TO POINTER-BYTE2.
008700     MOVE MEMORY-BYTE (19) TO POINTER-BYTE3.
008800     MOVE MEMORY-BYTE (20) TO POINTER-BYTE4.
008900     MOVE POINTER-WORK TO CVTADR.
009000****************************************************************
009100* GET THE SYSTEM DATE FROM THE CVT. SYSTEM DATE IS MAINTAINED  *
009200* AT OFFSETS 57-59 (DECIMAL) IN THE CVT. IT IS A PACKED-DECIMAL*
009300* NUMBER CONTAINING THE CURRENT JULIAN DATE AS YYDDDF.         *
009400****************************************************************
009500     SET BYTE-NBR TO CVTADR.
009600     MOVE MEMORY-BYTE (BYTE-NBR + 57) TO DATE-BYTE1.
009700     MOVE MEMORY-BYTE (BYTE-NBR + 58) TO DATE-BYTE2.
009800     MOVE MEMORY-BYTE (BYTE-NBR + 59) TO DATE-BYTE3.
009900     MOVE MEMORY-BYTE (BYTE-NBR + 60) TO DATE-BYTE4.
010000****************************************************************
010100* PRINT THE SYSTEM DATE.                                       *
010200****************************************************************
010300     MOVE DATE-PACKED TO PRINT-DATE.
010400     WRITE PRINT-LINE.
010500     CLOSE PRINT-OUT.
010600     GOBACK.
