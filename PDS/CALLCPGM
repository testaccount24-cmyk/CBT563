000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.    CALLCPGM.
000300 AUTHOR. R THORNTON
000310**************************************************************
000400*REMARKS. CALL A C LANGUAGE SUB-PROGRAM FROM COBOL/390.      *
000410*         PASSING DATA FROM THE INPUT DATA FILE. THE INPUT   *
000411*         DATA IS PRINTED BEFORE THE CALL, AND MODIFIED DATA *
000412*         ON RETURN FROM THE C PROGRAM IS ALSO PRINTED.      *
000413*                                                            *
000414*     (1) DATA IS PASSED TO THE C PROGRAM USING BY REFERENCE *
000415*         (THE COBOL DEFAULT), SO THE C PROGRAM MUST IDENTIFY*
000416*         ALL ITS INPUT PARAMETERS AS POINTERS, AS THE       *
000417*         ADDRESS OF THE REAL DATA (NOT A COPY) IS PASSED.   *
000418*                                                            *
000420*     (2) COMPILE WITH NODLL                                 *
000421*                                                            *
000422*     (3) CALLS TO THE C PROGRAM MUST BE STATIC AS THIS SET  *
000423*         OF PROGRAMS IS CURRENTLY WRITTEN. WHEN A DYNAMIC   *
000424*         CALL IS MADE, THE JOB ABENDS S0C7 ON ENTRY TO THE  *
000425*         CALLED C LANGUAGE PROGRAM. I HAVE AN ETR TO IBM    *
000426*         ASKING FOR ASSISTANCE WITH THIS.                   *
000430**************************************************************
000500 ENVIRONMENT DIVISION.
000600 CONFIGURATION SECTION.
000700 INPUT-OUTPUT SECTION.
000710 FILE-CONTROL.
000720     SELECT INPUT-FILE ASSIGN TO INPUT1.
000740     SELECT PRINT-FILE ASSIGN TO PRINT1.
000800 DATA DIVISION.
000810 FILE SECTION.
000820 FD  INPUT-FILE
000821     BLOCK CONTAINS 0 RECORDS
000822     RECORD CONTAINS 80 CHARACTERS
000823     RECORDING MODE IS F
000830     LABEL RECORDS ARE STANDARD.
000840 01  INPUT-RECORD.                                                      10
000850     05  IN-CHAR               PIC X.
000860     05  FILLER                PIC X.
000861     05  IN-STRING             PIC X(6).
000862     05  FILLER                PIC X.
000870     05  IN-INT                PIC S9(4).
000871     05  FILLER                PIC X.
000872     05  IN-FLOAT              PIC S9(4)V9(3).
000880     05  FILLER                PIC X(59).
000883
000895 FD  PRINT-FILE
000896     BLOCK CONTAINS 0 RECORDS
000897     RECORD CONTAINS 133 CHARACTERS
000898     RECORDING MODE IS F
000899     LABEL RECORDS ARE STANDARD.
000900 01  PRINT-RECORD.                                                      10
000901   05  CARRIAGE-CONTROL-BYTE   PIC X.                                   20
000902   05  PRINT-DATA.                                                      20
000903       10  PRT-PGM-NAME        PIC X(8).
000904       10  FILLER              PIC X.
000905       10  PRT-DATA-REC.
000906           15  PRT-IN-CHAR         PIC X.
000907           15  PRT-PTR1            PIC XX.
000908           15  PRT-RET-CHAR        PIC X.
000909           15  FILLER              PIC X.
000910           15  PRT-IN-STRING       PIC X(6).
000911           15  PRT-PTR2            PIC XX.
000912           15  PRT-RET-STRING      PIC X(6).
000913           15  FILLER              PIC X.
000914           15  PRT-IN-INT          PIC Z,ZZ9-.
000915           15  PRT-PTR3            PIC XX.
000916           15  PRT-RET-INT         PIC Z,ZZ9-.
000917           15  FILLER              PIC X.
000918           15  PRT-IN-FLOAT        PIC Z,ZZ9.999-.
000919           15  PRT-PTR4            PIC XX.
000920           15  PRT-RET-FLOAT       PIC Z,ZZ9.999-.
000921           15  FILLER              PIC X.
000930           15  PRT-C-RETURN        PIC ZZZ9-.
000932           15  FILLER              PIC X(60).
000933
000940 WORKING-STORAGE SECTION.
001000 77  FILLER PIC X(36)  VALUE
001100     'CALLCPGM WORKING STORAGE BEGINS HERE'.
001200 77  SUB                       PIC S9(4) COMP VALUE +0.
001300 01  GENERAL-AREAS.
001301     05  COUNTER               PIC S9(8) COMP-3.
001302     05  CFUNC-NAME            PIC X(8) VALUE 'CFUNC   '.
001303     05  C-RETURN-DATA         PIC S9(9) COMP.
001304     05  WS-SHORT-INT          PIC S9(4) COMP.
001308     05  WS-INT                PIC S9(9) COMP.                          40
001310     05  WS-LONG-INT           PIC S9(9) COMP.
001312     05  WS-FLOAT              COMP-1.
001313     05  WS-DOUBLE             COMP-2.
001314     05  WS-POINTER            POINTER.
001315     05  WS-DECIMAL            PIC S9(5)V99 COMP-3.
001316     05  WS-STRUCT.
001317         10 WS-INT-ARRAY       OCCURS 10 TIMES
001318                               PIC S9(4) COMP.
001319         10 WS-STRING-ARRAY    OCCURS 15 TIMES
001320                               PIC X.
001321     05  WS-LINE-SPACING       PIC 9 VALUE 1.
001322     05  END-OF-INPUT-SWITCH   PIC X VALUE 'N'.
001323         88  END-OF-INPUT-DATA    VALUE IS 'Y'.
001324         88  MORE-DATA-TO-PROCESS VALUE IS 'N'.
001600
001700 PROCEDURE DIVISION.
001800
001900 A100-EXECUTIVE-CONTROL.
002000     PERFORM A100-INITIALIZATION.
002100     PERFORM B100-MAINLINE-PROCESSING UNTIL END-OF-INPUT-DATA.
002200     PERFORM Z100-END-OF-PROCESSING.
002300     GOBACK.
002400
002500 A100-INITIALIZATION.
002600     OPEN INPUT
002601            INPUT-FILE,
002610          OUTPUT
002612            PRINT-FILE.
002620     MOVE SPACES TO PRINT-RECORD.
002700
002800 B100-MAINLINE-PROCESSING.
002900     PERFORM C100-READ-INPUT-FILE THRU C100-EXIT.
002910     IF MORE-DATA-TO-PROCESS
003000        PERFORM D100-PROCESS-THE-RECORD THRU D100-EXIT.
003010
003020 C100-READ-INPUT-FILE.
003030     READ INPUT-FILE
003040         AT END MOVE 'Y' TO END-OF-INPUT-SWITCH.
003041     IF MORE-DATA-TO-PROCESS
003042        MOVE '+ + + + + + + + + + + + +' TO PRT-DATA-REC
003043        PERFORM Y100-PRINT-A-LINE THRU Y100-EXIT
003044        MOVE 'READ INPUT-RECORD BELOW: ' TO PRT-DATA-REC
003045        PERFORM Y100-PRINT-A-LINE THRU Y100-EXIT
003046        MOVE INPUT-RECORD TO PRT-DATA-REC
003047        PERFORM Y100-PRINT-A-LINE THRU Y100-EXIT.
003050 C100-EXIT. EXIT.
003060
003070 D100-PROCESS-THE-RECORD.
003071     MOVE SPACES TO PRINT-DATA.
003072     MOVE '->' TO PRT-PTR1, PRT-PTR2, PRT-PTR3, PRT-PTR4.
003073     MOVE IN-CHAR TO PRT-IN-CHAR.
003075     MOVE IN-STRING TO PRT-IN-STRING.
003077     MOVE IN-INT TO WS-SHORT-INT.
003078     MOVE WS-SHORT-INT TO PRT-IN-INT.
003080     MOVE IN-FLOAT TO WS-FLOAT.
003081     MOVE WS-FLOAT TO PRT-IN-FLOAT.
003083     MOVE +0 TO C-RETURN-DATA.
003085*    CALL CFUNC-NAME
003086     CALL 'CFUNC'
003087          USING IN-CHAR, IN-STRING, WS-SHORT-INT, WS-FLOAT,
003088          RETURNING C-RETURN-DATA.
003089     MOVE IN-CHAR TO PRT-RET-CHAR.
003090     MOVE IN-STRING TO PRT-RET-STRING.
003091     MOVE WS-SHORT-INT TO PRT-RET-INT.
003092     MOVE WS-FLOAT TO PRT-RET-FLOAT.
003093     MOVE C-RETURN-DATA TO PRT-C-RETURN.
003094     PERFORM Y100-PRINT-A-LINE THRU Y100-EXIT.
003096 D100-EXIT. EXIT.
003097
003102 Y100-PRINT-A-LINE.
003103     MOVE 'CALLCPGM' TO PRT-PGM-NAME.
003104     WRITE PRINT-RECORD AFTER ADVANCING WS-LINE-SPACING.
003105     MOVE SPACES TO PRINT-RECORD.
003106 Y100-EXIT. EXIT.
003107
003108 Z100-END-OF-PROCESSING.
003300     CLOSE INPUT-FILE,
003500           PRINT-FILE.
